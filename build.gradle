buildscript {
    ext.kotlin_version = '1.3.31'
    ext.dependency_plugin_version = '1.0.7.RELEASE'
    ext.spring_boot_version = '2.1.4.RELEASE'
    ext.spring_cloud_version = 'Greenwich.RELEASE'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:${dependency_plugin_version}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31' apply false
}

group 'io.jyasonx'
version '1.0.0-SNAPSHOT'

allprojects {
    version '1.0.0-SNAPSHOT'

    ext.axon_version = '4.1.1'
    ext.commons_vean_utils_version = '1.9.3'
    ext.commons_io_version = '2.6'
    ext.commons_compress_version = '1.18'
    ext.bouncycastle_version = '1.61'
    ext.embedded_kafka_version = '2.2.0'
    ext.embedded_mongodb_version = '2.2.0'
    ext.embedded_rabbitmq_version = '1.3.0'
    ext.embedded_redis_version = '0.7.2'
    ext.groovy_version = '2.5.6'
    ext.guava_version = '27.1-jre'
    ext.h2_version = '1.4.199'
    ext.hibernate_validator_version = '6.0.16.Final'
    ext.mapstruct_version = '1.3.0.Final'
    ext.scala_version = '2.12'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    apply plugin: 'maven'
    apply plugin: 'io.spring.dependency-management'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    configurations {
        all*.exclude module: 'spring-boot-starter-tomcat'
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        // fix gradle build problem:
        // 'The following annotation processors were detected on the compile classpath:
        // 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor' and
        // 'lombok.launch.AnnotationProcessorHider$ClaimingProcessor'.
        // Detecting annotation processors on the compile classpath is deprecated and Gradle 5.0 will ignore them.
        // Please add them to the annotation processor path instead.
        // If you did not intend to use annotation processors,
        // you can use the '-proc:none' compiler argument to ignore them'
        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${spring_boot_version}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
        }

        dependencies {
            // commons
            dependency "com.google.guava:guava:${guava_version}"
            dependency "commons-beanutils:commons-beanutils:${commons_vean_utils_version}"
            dependency "commons-io:commons-io:${commons_io_version}"
            dependency "org.apache.commons:commons-compress:${commons_compress_version}"
            dependency "org.hibernate:hibernate-validator:${hibernate_validator_version}"
            dependency "org.mapstruct:mapstruct-jdk8:${mapstruct_version}"
            dependency "org.mapstruct:mapstruct-processor:${mapstruct_version}"
            dependency "org.codehaus.groovy:groovy-all:${groovy_version}"

            // bouncy castle
            dependency "org.bouncycastle:bcpkix-jdk15on:${bouncycastle_version}"
            dependency "org.bouncycastle:bcmail-jdk15on:${bouncycastle_version}"
            dependency "org.bouncycastle:bcpg-jdk15on:${bouncycastle_version}"
            dependency "org.bouncycastle:bctls-jdk15on:${bouncycastle_version}"
            dependency "org.bouncycastle:bcprov-ext-jdk15on:${bouncycastle_version}"

            // embedded server
            dependency "de.flapdoodle.embed:de.flapdoodle.embed.mongo:${embedded_mongodb_version}"
            dependency "com.h2database:h2:${h2_version}"
            dependency "it.ozimov:embedded-redis:${embedded_redis_version}"
            dependency "io.arivera.oss:embedded-rabbitmq:${embedded_rabbitmq_version}"
            dependency "io.github.embeddedkafka:embedded-kafka_${scala_version}:${embedded_kafka_version}"

            // axon
            dependency "org.axonframework:axon-messaging:${axon_version}"
            dependency "org.axonframework:axon-modelling:${axon_version}"
            dependency "org.axonframework:axon-eventsourcing:${axon_version}"
            dependency "org.axonframework:axon-configuration:${axon_version}"
            dependency "org.axonframework:axon-test:${axon_version}"
            dependency "org.axonframework:axon-server-connector:${axon_version}"
            dependency "org.axonframework:axon-spring:${axon_version}"
            dependency "org.axonframework:axon-spring-boot-starter:${axon_version}"
            dependency "org.axonframework:axon-disruptor:${axon_version}"
            dependency "org.axonframework:axon-metrics:${axon_version}"
            dependency "org.axonframework:axon-cdi:${axon_version}"
            dependency "org.axonframework:axon-legacy:${axon_version}"

            dependency "org.axonframework.extensions.amqp:axon-amqp:${axon_version}"
            dependency "org.axonframework.extensions.amqp:axon-amqp-spring-boot-starter:${axon_version}"
            dependency "org.axonframework.extensions.kafka:axon-kafka:${axon_version}"
            dependency "org.axonframework.extensions.kafka:axon-kafka-spring-boot-starter:${axon_version}"
            dependency "org.axonframework.extensions.springcloud:axon-springcloud:${axon_version}"
            dependency "org.axonframework.extensions.springcloud:axon-springcloud-spring-boot-starter:${axon_version}"
            dependency "org.axonframework.extensions.jgroups:axon-jgroups:${axon_version}"
            dependency "org.axonframework.extensions.jgroups:axon-jgroups-spring-boot-starter:${axon_version}"
            dependency "org.axonframework.extensions.mongo:axon-mongo:${axon_version}"

            dependency "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
            dependency "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"
            dependency "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
            dependency "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
            dependency "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"

        }

    }

    repositories {
        jcenter()
    }

    // see warning.detail
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
        }
    }
}

configure(subprojects.findAll() {it.name == 'jsxpay-gateway'}) {
    
    apply plugin: 'org.springframework.boot'
    
}
